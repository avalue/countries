apply plugin: 'com.android.application'
apply plugin: 'realm-android'
apply plugin: 'com.getkeepsafe.dexcount'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.0"
    defaultConfig {
        applicationId "com.patloew.countries"
        minSdkVersion 19
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        vectorDrawables.useSupportLibrary = true

        setProperty("archivesBaseName", "Countries-v$versionName-b$versionCode")
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles 'proguard-rules.pro'
        }
    }
    flavorDimensions "default"
    productFlavors {
        devBackend {
            dimension "default"
            applicationIdSuffix ".dev"
            resValue "string", "app_name", "Countries Dev"
            buildConfigField "String", "BASE_URL", "\"https://restcountries.eu/\""
        }
        productionBackend {
            dimension "default"
            resValue "string", "app_name", "Countries"
            buildConfigField "String", "BASE_URL", "\"https://restcountries.eu/\""
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dataBinding {
        enabled = true
    }
    lintOptions {
        abortOnError false
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    packagingOptions {
        // Remove unneeded Realm native libs (nearly no devices use these)
        exclude 'lib/mips/librealm-jni.so'
        exclude 'lib/x86_64/librealm-jni.so'
    }
}

ext.supportLibVersion = '26.0.0-beta2' // https://developer.android.com/topic/libraries/support-library/revisions.html
ext.gsonVersion = '2.8.1' // https://github.com/google/gson
ext.retrofitVersion = '2.3.0' // http://square.github.io/retrofit/
ext.okHttpVersion = '3.8.1' // http://square.github.io/okhttp/
ext.okHttpIdlingResourceVersion = '1.0.0' // https://github.com/JakeWharton/okhttp-idling-resource
ext.recyclerviewFastscrollVersion = '1.0.16' // https://github.com/timusus/RecyclerView-FastScroll
ext.timberVersion = '4.5.1' // https://github.com/JakeWharton/timber
ext.parcelerVersion = '1.1.9' // http://parceler.org/
ext.daggerVersion = '2.11' // https://google.github.io/dagger/
ext.rxJavaVersion = '2.1.1' // https://github.com/ReactiveX/RxJava
ext.rxAndroidVersion = '2.0.1' // https://github.com/ReactiveX/RxAndroid
ext.aboutLibrariesVersion = '5.9.6' // https://github.com/mikepenz/AboutLibraries
ext.leakCanaryVersion = '1.5.1' // https://github.com/square/leakcanary
ext.junitVersion = '4.12' // http://junit.org/junit4/
ext.mockitoVersion = '2.8.47' // http://site.mockito.org/
ext.powermockVersion = '1.7.0' // https://github.com/powermock/powermock
ext.testRunnerVersion = '0.5' // https://google.github.io/android-testing-support-library/downloads/
ext.testRulesVersion = testRunnerVersion
ext.espressoVersion = '2.2.2' // https://google.github.io/android-testing-support-library/downloads/

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "com.android.support:support-fragment:$supportLibVersion"
    implementation "com.android.support:support-core-ui:$supportLibVersion"
    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation "com.android.support:cardview-v7:$supportLibVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibVersion"
    implementation "com.android.support:design:$supportLibVersion"

    implementation "com.google.code.gson:gson:$gsonVersion"

    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
    implementation "com.squareup.okhttp3:okhttp:$okHttpVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"

    implementation "com.simplecityapps:recyclerview-fastscroll:$recyclerviewFastscrollVersion"

    implementation "com.jakewharton.timber:timber:$timberVersion"

    implementation "org.parceler:parceler-api:$parcelerVersion"
    annotationProcessor "org.parceler:parceler:$parcelerVersion"

    annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"
    implementation "com.google.dagger:dagger:$daggerVersion"

    implementation "io.reactivex.rxjava2:rxjava:$rxJavaVersion"
    implementation "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"
    compileOnly "io.reactivex:rxjava:1.3.0"

    implementation("com.mikepenz:aboutlibraries:$aboutLibrariesVersion@aar") {
        transitive = true
    }

    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"

    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "org.powermock:powermock-module-junit4:$powermockVersion"
    testImplementation "org.powermock:powermock-module-junit4-rule:$powermockVersion"
    testImplementation "org.powermock:powermock-api-mockito2:$powermockVersion"
    //testImplementation 'io.reactivex:rxjava:1.3.0'

    androidTestImplementation "com.android.support:support-annotations:$supportLibVersion"
    androidTestImplementation "com.android.support.test:runner:$testRunnerVersion"
    androidTestImplementation "com.android.support.test:rules:$testRulesVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$espressoVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-intents:$espressoVersion"
    androidTestImplementation("com.android.support.test.espresso:espresso-contrib:$espressoVersion") {
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
    }
    androidTestImplementation "com.squareup.okhttp3:okhttp:$okHttpVersion"
    androidTestImplementation "com.jakewharton.espresso:okhttp3-idling-resource:$okHttpIdlingResourceVersion"
}
